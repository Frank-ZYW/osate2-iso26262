package aadlbook::platform

public

	with SEI;
	with emv2;

	processor ecu
		features
			socket1: requires bus access can;
			socket2: requires bus access can;
		properties
			SEI::MIPSCapacity => 50.0 MIPS;
		annex EMV2 {**
	use types 		ErrorLibrary;
	use behavior  	lib::FailStop;

	error propagations
		bindings : out propagation {ServiceError};
	flows
		fsoft : error source bindings{ServiceError};
		fhard : error source bindings{ServiceError};
	end propagations;

	component error behavior
	events
		SoftwareError : error event;
		HardwareError : error event;
		Reset         : recover event;
	transitions
		Operational -[SoftwareError]-> FailStop; 
		Operational -[HardwareError]-> FailStop;
		FailStop      -[Reset]->         Operational;
	propagations
		normal       : Operational -[]-> bindings {NoError};
		fs           : FailStop -[]-> bindings{ServiceError};  
	end component;
	properties
  		ISO26262::hazards =>([ HazardName =>"Ecu failure";
						  SafetyDescription=>"Perform intelligent vehicle control function";]) applies to FailStop; 
  		ISO26262::hazards =>([ HazardName =>"Hardware failure from the platform (OS exception, etc.)";
						  SafetyDescription=>"Normal execution of platform hardware";
						  Comment=>"Impact all components that are controlled by this software";]) applies to SoftwareError; 
  		ISO26262::hazards =>([ HazardName =>"Software failure from the platform (OS exception, etc.)";
						  SafetyDescription=>"Normal operation of platform software";
						  Comment=>"Impact all components that are controlled by this processor";]) applies to HardwareError; 
		EMV2::OccurrenceDistribution => [ ProbabilityValue => 1.35e-5 ; Distribution => Poisson;] applies to HardwareError;
**};
	end ecu;

	bus can
		properties
			Latency => 1Ms .. 1Ms;
			SEI::BandWidthCapacity => 500000.0 bitsps;
			Transmission_Time => [Fixed => 10ms .. 30ms;
				PerByte => 1us .. 10us;];
	end can;

	bus rs232
		properties
			Latency => 1Ms .. 1Ms;
			SEI::BandWidthCapacity => 5.0 KBytesps;
			Transmission_Time => [Fixed => 200ms .. 500ms;
				PerByte => 2ms .. 5ms;];
	end rs232;

	bus bluetooth
	end bluetooth;

end aadlbook::platform;
