package aadlbook::software::panel_control

public

with aadlbook::icd;

process panel_control
features
	increase_speed : in event port;
	decrease_speed : in event port;
	current_speed : in data port aadlbook::icd::speed;
	desired_speed : out data port aadlbook::icd::speed;
	tire_pressure_in : in data port aadlbook::icd::pressure;
	tire_pressure_out : out data port aadlbook::icd::pressure;
flows
	f99 : flow path increase_speed -> desired_speed;
	f31 : flow path decrease_speed -> desired_speed;
	f32 : flow path current_speed -> desired_speed;
	f33 : flow path tire_pressure_in -> tire_pressure_out; 
annex EMV2 {**
	use types 		ErrorLibrary;
	use behavior  	lib::FailStop;

	error propagations
		current_speed : in propagation {ItemOmission,OutOfRange};
		desired_speed : out propagation {ItemOmission,OutOfRange};
		tire_pressure_out : out propagation {OutOfRange};
		increase_speed : in propagation {ItemOmission};
		decrease_speed : in propagation {ItemOmission};
		tire_pressure_in : in propagation {OutOfRange};
	flows
		ef0 : error path current_speed{ItemOmission} -> desired_speed{ItemOmission};
		ef1 : error path current_speed{OutOfRange} -> desired_speed{OutOfRange};
		ef2 : error path tire_pressure_in{OutOfRange} -> desired_speed{OutOfRange};
		ef3 : error path increase_speed{ItemOmission} -> desired_speed{OutOfRange};
		ef4 : error path decrease_speed{ItemOmission} -> desired_speed{OutOfRange};
		ef5 : error source desired_speed{OutOfRange};
		ef6 : error source desired_speed{ItemOmission};
	end propagations;
	properties
		ISO26262::hazards =>([ HazardName =>"Unable to produce the desired speed";
				  SafetyDescription=>"Produces the desired speed and information";]) applies to  FailStop;
		ISO26262::hazards =>([ HazardName =>"Loss of desired speed";
				  SafetyDescription=>"Output the desired speed";]) applies to  desired_speed.ItemOmission; 
		ISO26262::hazards =>([ HazardName =>"Invalid desired speed";
				  SafetyDescription=>"Output the desired speed";]) applies to  desired_speed.OutOfRange;
		ISO26262::hazards =>([ HazardName =>"Invalid tire pressure";
				  SafetyDescription=>"Output the  tire pressure";]) applies to  tire_pressure_out.OutOfRange;  
		ISO26262::FMEDASafetyProperties => [FailureRate => 1.0; SafetyRelated => true;] applies to FmedaState;	
		ISO26262::FailureMode => [	ModeName => "Unable to handle and output the correct speed";
				  	  			 	Distribution => 100.0;
				  	  				Violate_SP_Satety => true;
				  	  				SPF_SM => "SM2";
					  				SPF_DC => 95.0;
				 	  				Violate_MP_Satety => false;
				  				] applies to FailStop;
		EMV2::OccurrenceDistribution => [ ProbabilityValue => 0.5e-5 ; Distribution => Poisson;] applies to ef5;
		EMV2::OccurrenceDistribution => [ ProbabilityValue => 0.5e-5 ; Distribution => Poisson;] applies to ef6;
		EMV2::OccurrenceDistribution => [ ProbabilityValue => 0.5e-5 ; Distribution => Poisson;] applies to tire_pressure_in;
		EMV2::OccurrenceDistribution => [ ProbabilityValue => 0.5e-5 ; Distribution => Poisson;] applies to tire_pressure_out;
		EMV2::OccurrenceDistribution => [ ProbabilityValue => 0.5e-5 ; Distribution => Poisson;] applies to Failure;
**};
end panel_control;

process implementation panel_control.i
subcomponents
	thr : thread panel_control_thr;
connections
	c0 : port increase_speed -> thr.increase_speed;
	c1 : port decrease_speed -> thr.decrease_speed;
	c2 : port current_speed -> thr.current_speed;
	c3 : port tire_pressure_in -> thr.tire_pressure_in;
	c4 : port thr.tire_pressure_out -> tire_pressure_out;
	c5 : port thr.desired_speed -> desired_speed;
flows
	f99 : flow path increase_speed -> c0 -> thr.f40 -> c5 -> desired_speed;
	f31 : flow path decrease_speed -> c1 -> thr.f41 -> c5 -> desired_speed;
	f32 : flow path current_speed -> c2 -> thr.f42 -> c5 -> desired_speed;
	f33 : flow path tire_pressure_in -> c3 -> thr.f43 -> c4 -> tire_pressure_out; 
end panel_control.i;

thread panel_control_thr
features
	increase_speed : in event port;
	decrease_speed : in event port;
	current_speed : in data port aadlbook::icd::speed;
	desired_speed : out data port aadlbook::icd::speed;
	tire_pressure_in : in data port aadlbook::icd::pressure;
	tire_pressure_out : out data port aadlbook::icd::pressure;
flows
	f40 : flow path increase_speed -> desired_speed;
	f41 : flow path decrease_speed -> desired_speed;
	f42 : flow path current_speed -> desired_speed;
	f43 : flow path tire_pressure_in -> tire_pressure_out; 
end panel_control_thr;

end aadlbook::software::panel_control;
