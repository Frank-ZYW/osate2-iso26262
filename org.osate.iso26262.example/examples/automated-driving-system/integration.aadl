package aadlbook::integration

public

	with aadlbook::devices;
	with aadlbook::software::image_acquisition;
	with aadlbook::software::obstacle_detection;
	with aadlbook::software::panel_control;
	with aadlbook::software::speed_controller;
	with aadlbook::software::speed_voter;
	with aadlbook::software::entertainment;
	with aadlbook::platform;
	with ISO26262;

	system integration
	features
		bluetooth: requires bus access aadlbook::platform::bluetooth;
		external: in data port;

	annex EMV2 {**
	use types 		ErrorLibrary;
	use behavior  	lib::FailStop;
	
	error propagations
		external: in propagation {CommonErrors};
	flows
		ef0:error sink external{CommonErrors};
	end propagations;
	
	component error behavior
		transitions
			Operational -[external{CommonErrors}]-> FailStop;
	end component;
    properties
    ISO26262::FmeaRiskAnalysis=>[ Severity=>9; ] applies to FailStop;  
    ISO26262::hazards =>([ HazardName => "Automatic driving function failure";
						   Mishap=>"Hit other vehicles or obstacles or pedestrians";
						   Environment=>"All driving scenarios during vehicle exercise";
						   Severity=>S3;SeverityComment=>"The vehicle loses stability, and the driver has life-threatening hazards";
						   Controllability=>C3;ControllabilityComment=>"Both driver and pedestrian are uncontrollable";
						   Exposure=>E4;ExposureComment=>"High scene exposure";
						   ASIL=>D;
						   SafetyDescription=>"The car can safely perform automatic driving";
						   SafetyCategory=>SG;]) applies to FailStop; 
    **};
	end integration;

	system implementation integration.functional0
		subcomponents
			-- input devices
			obstacle_camera: device aadlbook::devices::camera0;
			wheel_sensor: device aadlbook::devices::speed_wheel_sensor0;
			panel: device aadlbook::devices::panel;
			tire_pressure: device aadlbook::devices::tpms;
			bluetooth_ctrl: device aadlbook::devices::bluetooth_controller;

			-- software and processing elements.
			image_acquisition: process aadlbook::software::image_acquisition::image_acquisition.i;
			panel_controller: process aadlbook::software::panel_control::panel_control.i;
			speed_ctrl: process aadlbook::software::speed_controller::speed_controller.i;
			entertainment: process aadlbook::software::entertainment::entertainment.i;

			-- output devices
			brake: device aadlbook::devices::brake;
			acceleration: device aadlbook::devices::acceleration;
			screen: device aadlbook::devices::screen;
			speaker: device aadlbook::devices::speaker;
		connections
			c00: port obstacle_camera.picture -> image_acquisition.picture;
			c01: port image_acquisition.obstacle_detected -> speed_ctrl.obstacle_position;
			c04: port wheel_sensor.speed -> speed_ctrl.current_speed;
			c05: port wheel_sensor.speed -> screen.actual_speed;
			c08: port speed_ctrl.speed_cmd -> acceleration.cmd;
			c09: port speed_ctrl.brake_cmd -> brake.cmd;
			c10: port speed_ctrl.warning -> screen.warning;
			c11: port panel.increase_speed -> panel_controller.increase_speed;
			c12: port panel.decrease_speed -> panel_controller.decrease_speed;
			c13: port panel_controller.desired_speed -> speed_ctrl.desired_speed;
			c14: port wheel_sensor.speed -> panel_controller.current_speed;
			c15: port panel_controller.desired_speed -> screen.desired_speed;
			c16: port bluetooth_ctrl.contacts -> entertainment.contacts;
			c17: port bluetooth_ctrl.music -> entertainment.music_in;
			c18: port entertainment.music_out -> speaker.music;
			c19: port entertainment.infos -> screen.entertainment_infos;
			c20: port panel_controller.tire_pressure_out -> screen.tire_pressure;
			c21: port tire_pressure.pressure -> panel_controller.tire_pressure_in;
			c22: bus access bluetooth <-> bluetooth_ctrl.bt;
		annex EMV2 {**
			composite error behavior
			states
				[brake.FailStop and acceleration.FailStop]-> FailStop;
			end composite;
		**};
	end integration.functional0;

	-- Add sensor and camera redundancy
	system implementation integration.functional
		subcomponents
			-- input devices
			obstacle_camera    : device aadlbook::devices::camera;
			obstacle_radar     : device aadlbook::devices::radar;
			wheel_sensor       : device aadlbook::devices::speed_wheel_sensor;
			laser_sensor       : device aadlbook::devices::speed_laser_sensor;
			panel              : device aadlbook::devices::panel;
			tire_pressure      : device aadlbook::devices::tpms;
			bluetooth_ctrl     : device aadlbook::devices::bluetooth_controller;
		
			-- software and processing elements.
		    image_acquisition  : process aadlbook::software::image_acquisition::image_acquisition.i;
		    obstacle_detection : process aadlbook::software::obstacle_detection::obstacle_detection.i;
		    panel_controller   : process aadlbook::software::panel_control::panel_control.i;
		    speed_voter        : process aadlbook::software::speed_voter::speed_voter.i;
		    speed_ctrl         : process aadlbook::software::speed_controller::speed_controller.i;
		    entertainment      : process aadlbook::software::entertainment::entertainment.i;
		
			-- output devices
			brake         : device aadlbook::devices::brake;
			acceleration  : device aadlbook::devices::acceleration;
			screen        : device aadlbook::devices::screen;
			speaker       : device aadlbook::devices::speaker;
		connections
		    c00 : port obstacle_camera.picture               -> image_acquisition.picture;
		    c01 : port image_acquisition.obstacle_detected   -> obstacle_detection.camera; 
		    c02 : port obstacle_radar.distance_estimate      -> obstacle_detection.radar;
		    c03 : port obstacle_detection.obstacle_position  -> speed_ctrl.obstacle_position;
		    c04 : port wheel_sensor.speed                    -> speed_voter.wheel_sensor;
		    c05 : port laser_sensor.speed                    -> speed_voter.laser_sensor;
		    c06 : port speed_voter.speed                     -> speed_ctrl.current_speed;
		    c07 : port speed_voter.speed                     -> screen.actual_speed;
		    c08 : port speed_ctrl.speed_cmd                  -> acceleration.cmd;
		    c09 : port speed_ctrl.brake_cmd                  -> brake.cmd;
		    c10 : port speed_ctrl.warning                    -> screen.warning;
		    c11 : port panel.increase_speed                  -> panel_controller.increase_speed;
		    c12 : port panel.decrease_speed                  -> panel_controller.decrease_speed;
		    c13 : port panel_controller.desired_speed        -> speed_ctrl.desired_speed;
		    c14 : port speed_voter.speed                     -> panel_controller.current_speed;
		    c15 : port panel_controller.desired_speed        -> screen.desired_speed;
			c16 : port bluetooth_ctrl.contacts               -> entertainment.contacts;
			c17 : port bluetooth_ctrl.music                  -> entertainment.music_in;
			c18 : port entertainment.music_out               -> speaker.music;
			c19 : port entertainment.infos                   -> screen.entertainment_infos;
			c20 : port panel_controller.tire_pressure_out    -> screen.tire_pressure;
			c21 : port tire_pressure.pressure                -> panel_controller.tire_pressure_in;
			c22 : bus access bluetooth                       <-> bluetooth_ctrl.bt;
		properties
			ISO26262::FmeaHead => [ Subject => "Automotive System.";
								    DFMEAStartData => "2022/3/1";
								    DFMEARevisionData => "2023/3/1";
								    FocusComponent => "speed_ctrl";];
		annex EMV2 {**
			composite error behavior
			states
				[brake.FailStop and acceleration.FailStop]-> FailStop;
			end composite;
				
		**};						 	
end integration.functional;

end aadlbook::integration;
